set xdata time
set timefmt '"%m/%d/%Y"'
set grid
set autoscale fix
set xtics rangelimited rotate by -55 format '%1m/%1d'
set title ARG1

set terminal dumb
set output '/dev/null'

plot [:][:] 'ratio' using 1:2 with lines notitle\
, '' using 1:2:3 with labels right offset -.5,.25 font ',7' point pt 6 notitle\
, '' using 1:2:4 with labels center offset 0,-.75 font ',7' point pt 6 notitle

set terminal pngcairo size 820,440 transparent
set output "ratio.png"

# 86400 = 24 * 60 * 60; 2678400 = 31 * 86400
interval = int((GPVAL_X_MAX - GPVAL_X_MIN)/2678400) + 1
set xtics interval * 86400
set mxtics interval

refresh
