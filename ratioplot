set border 11 front lc rgbcolor 'gray'
set xdata time
set timefmt '"%m/%d/%Y"'
set autoscale fix
set title ARG1

unset y2label

set xtics nomirror
set x2tics scale 0
set border 11

set terminal dumb
set output '/dev/null'

plot [:][:] 'ratio' u 1:2 w lines smooth csplines lt 0 lc 6 notitle\
, '' u 1:2 w points lt -1 lc 6 pt 1 ps .5 notitle\
, '' u 1:2:3 w labels right offset -.5,.25 font ',7' notitle\
, '' u 1:($3 != NaN ? $2 : NaN) w points lt -1 lc 6 pt 1 notitle

set terminal pngcairo size 820,440 transparent
set output "ratio.png"

# 86400 = 24 * 60 * 60; 2678400 = 31 * 86400
interval = int((GPVAL_X_MAX - GPVAL_X_MIN)/2678400) + 1
set x2tics interval * 86400 format ''
set mx2tics interval

# only label Mondays
set xtics '"01/06/2020"', 604800

set grid x2 y

refresh
