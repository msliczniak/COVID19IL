set xdata time
set timefmt '"%m/%d/%Y"'
set autoscale fix
set title ARG1

unset y2label

set xtics nomirror
set x2tics scale 0
set border 11

set terminal dumb
set output '/dev/null'

plot [:][:] 'ratio' using 1:2 with lines notitle\
, '' using 1:2:3 with labels right offset -.5,.25 font ',7' point pt 0 notitle\
, '' using 1:2:4 with labels center offset 0,-.75 font ',7' point pt 0 notitle

set terminal pngcairo size 820,440 transparent
set output "ratio.png"

# 86400 = 24 * 60 * 60; 2678400 = 31 * 86400
interval = int((GPVAL_X_MAX - GPVAL_X_MIN)/2678400) + 1
set x2tics interval * 86400 format ''
set mx2tics interval

# only label Mondays
set xtics '"01/06/2020"', 604800

set grid x2 y

refresh
