set border 11 front lc rgbcolor 'gray'
set xdata time
set timefmt '"%m/%d/%Y"'
set autoscale fix
set title ARG1

unset y2label

set xtics nomirror
set x2tics scale 0
set border 11

set terminal dumb
set output '/dev/null'

set clip two
plot 'ratio' u 1:2 w points notitle

set xrange   [GPVAL_X_MIN:GPVAL_X_MAX]
set yrange   [GPVAL_Y_MIN:GPVAL_Y_MAX]

set terminal pngcairo size 1640,440 transparent
set output "ratio.png"

# 86400 = 24 * 60 * 60; 2678400 = 31 * 86400
interval = int((GPVAL_X_MAX - GPVAL_X_MIN)/2678400) + 1
set x2tics interval * 86400 format ''
set mx2tics interval

# only label Mondays
set xtics '"01/06/2020"', 604800

# shade alternating weeks
#     January 1970
# Su Mo Tu We Th Fr Sa
#              1  2  3
# 604800 = 60 * 60 * 24 * 7; 1209600 = 604800 * 2
# day of week offset
dowo = 86400 * (int(GPVAL_X_MAX / 86400 + 2.5) % 7 + 4)
set for [i=GPVAL_X_MAX-dowo+604800:GPVAL_X_MIN:-1209600] obj i\
  rect from i-604800,graph 0 to i,graph 1\
  fc 'gray' fs transparent noborder bgnd back

set logscale y
set grid x2 y

plot 'ratio' u 1:2 w lines smooth csplines lt 0 lc 6 notitle\
, '' u 1:2 w points lt -1 lc 6 pt 1 ps .5 notitle\
, '' u 1:2:3 w labels right offset -.5,.25 font ',7' notitle\
, '' u 1:($3 != NaN ? $2 : NaN) w points lt -1 lc 6 pt 1 notitle
