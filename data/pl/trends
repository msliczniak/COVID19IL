set xdata time
set timefmt '%Y-%m-%d-%H'
set grid
#set xtics rangelimited rotate by -55 format '%1m/%1d'
set autoscale fix
set xtics rotate by -55 format '%1d.%1m'
set xlabel ARG3.' dn czas podwojenia'
set key outside horizontal bottom center
set ytics nomirror
set logscale y
set format y '10^{%T}'
set ylabel ARG2
#set logscale y2
#set y2range [*:*] reverse
set title ARG1
set y2label 'czas podwojenia (godz)'
set y2tics 24
set my2tics 2
set terminal dumb
set output '/dev/null'
plot \
'tot' using 1:2 axis x1y1 with linespoints notitle, \
'pred' using 1:2 axis x1y1 with lines notitle, \
'5' using 1:2 axis x1y2 with points title '5 prób =', \
'10' using 1:2 axis x1y2 with points title '10 prób =', \
'15' using 1:2 axis x1y2 with points title '15 prób =', \
'dhrs' using 1:2 axis x1y2 with linespoints title 'pełny termin ='

# 86400 = 24 * 60 * 60; 2678400 = 31 * 86400
interval = int((GPVAL_X_MAX - GPVAL_X_MIN)/2678400) + 1
set xtics interval * 86400
set mxtics interval

set terminal pngcairo size 820,440
set output 'a.png'
replot

