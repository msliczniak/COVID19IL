#!/usr/bin/awk -f

BEGIN { FS = "," }

function which(k, i) {
        for (i = 1; i <= NF; i++) if (tolower($i) ~ k) return (i)
        exit (1)
}

# days since epoch
function dse(d, f, c) {
	if (!d) return ("")

	f = "%m/%d/%Y"
	c = da[d]
	if (c) return (c)

	f = sprintf("/bin/date -juf '%s-%%T' %s-00:00:00 '+%%s'", f, d)
	f | getline c
	close(f)

	c = int(c / 86400)
	da[d] = c ""
	return (c)
}

FNR == 1 {
	close(cmd)

	age = which("age$")
	death = which("death")
	date = which("date$")
	sex = which("sex")
	city = which("city")
	onset = which("onset")

	# strip-off path
	d = match(FILENAME, "[^/]*$")
	d = substr(substr(FILENAME, d), 1)

	# get date
	if (d !~ /^[1-9][0-9][0-9][0-9]-/)
		d = substr(d, index(d, "-") + 1, 10)
	else
		d = substr(d, 1, 10)

	cmd = sprintf("/usr/bin/sort >deaths-%s.txt", d)

	next
}

$death {
	printf("%20d %3d%s %s,%s\n", dse($date), $age,
	  tolower(substr($sex, 1, 1)), $city, dse($onset)) | cmd
}
