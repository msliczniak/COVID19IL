#!/usr/bin/awk -f

BEGIN { FS = "," }

function which(k, i) {
        for (i = 1; i <= NF; i++) if (tolower($i) ~ k) return (i)
        exit (1)
}

# days since epoch
function dse(d, f, c) {
	if (!d) return ("")

	f = "%m/%d/%Y"
	c = da[d]
	if (c) return (c)

	f = sprintf("/bin/date -juf '%s-%%T' %s-00:00:00 '+%%s'", f, d)
	f | getline c
	close(f)

	c = int(c / 86400)
	da[d] = c ""
	return (c)
}

FNR == 1 {
	age = which("age$")
	death = which("death")
	date = which("date$")
	sex = which("sex")
	city = which("city")
	onset = which("onset")

	next
}

FNR == 2 {
	close(cmd)

	# strip-off path
	d = match(FILENAME, "[^/]*$")
	d = substr(substr(FILENAME, d), 1)

	# get date
	if (d !~ /^[1-9][0-9][0-9][0-9]-/)
		d = substr(d, index(d, "-") + 1, 10)
	else
		d = substr(d, 1, 10)

	d = sprintf("report-%s.csv", d)
	cmd = sprintf("/usr/bin/sort -t, -k4,4 -k1,1 -k2,2 -k5,5 -k3,3 >%s", d)
	rfiles = d "\n" rfiles
}

{
	if ($death) d = 1
	else d = 0

	s = tolower(substr($sex, 1, 1))
	if (s == "") s = "u"

	cn = tolower($city)
	if (cn == "chicago") cn = "w chicago"
	else if (cn == "il") cn = ""
	else if (cn ~ /^unk/) cn = ""
	else if (cn ~ /^saint /) cn = "st" substr(cn, 6)
	else if (cn ~ /^north /) cn = "n" substr(cn, 6)
	else if (cn ~ /^east /) cn = "e" substr(cn, 5)
	else if (cn ~ /^south /) cn = "s" substr(cn, 6)
	else if (cn ~ /^west /) cn = "w" substr(cn, 5)

	gsub(/\./, "", cn)
	gsub(/apt ([^ ]*)/, "", cn)
	sub(/^ */, "", cn)
	sub(/ *$/, "", cn)
	gsub(/ ( *)/, " ", cn)

	if (cn == "apt 1a") cn = ""
	if (cn == "campbell hill") cn = "campton hills"
	else if (cn == "capertersville") cn = "carpentersville"
	else if (cn == "carpentersuile") cn = "carpentersville"
	else if (cn == "cerpentersville") cn = "carpentersville"
	else if (cn == "dundee") cn = "dundee twp"
	else if (cn == "eltgin") cn = "elgin"
	else if (cn == "relgin") cn = "elgin"
	else if (cn == "st xharles") cn = "st charles"
	else if (cn == "urora") cn = "aurora"

	printf("%d,%020d,%03d%s,%s,%s\n", d, dse($date), $age,
	  s, cn, dse($onset)) | cmd
}

END {
	printf("%s", rfiles)
}
