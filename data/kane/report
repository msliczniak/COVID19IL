#!/usr/bin/awk -f

BEGIN { FS = "," }

function which(k, i) {
        for (i = 1; i <= NF; i++) if (tolower($i) ~ k) return (i)
        exit (1)
}

# days since epoch
function dse(d, f, c) {
	if (!d) return ("")

	f = "%m/%d/%Y"
	c = da[d]
	if (c) return (c)

	f = sprintf("/bin/date -juf '%s-%%T' %s-00:00:00 '+%%s'", f, d)
	f | getline c
	close(f)

	c = int(c / 86400)
	da[d] = c ""
	return (c)
}

FNR == 1 {
	age = which("age$")
	death = which("death")
	date = which("date$")
	sex = which("sex")
	city = which("city")
	onset = which("onset")

	next
}

FNR == 2 {
	close(cmd)

	# strip-off path
	d = match(FILENAME, "[^/]*$")
	d = substr(substr(FILENAME, d), 1)

	# get date
	if (d !~ /^[1-9][0-9][0-9][0-9]-/)
		d = substr(d, index(d, "-") + 1, 10)
	else
		d = substr(d, 1, 10)

	d = sprintf("report-%s.txt", d)
	cmd = sprintf("/usr/bin/sort >%s", d)
	rfiles = d "\n" rfiles
}

{
	if ($death) d = 1
	else d = 0

	s = tolower(substr($sex, 1, 1))
	if (s == "") s = "u"

	printf("%d %20d %3d%s %s,%s\n", d, dse($date), $age,
	  s, $city, dse($onset)) | cmd
}

END {
	printf("%s", rfiles)
}
