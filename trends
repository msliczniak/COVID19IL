trend_t = ARG3 . xlabel_t . ', ' . trend_t

set border 11 back lc rgbcolor 'gray'
set rmargin 6

set xdata time
set timefmt '%Y-%m-%d-%H'
set autoscale fix
set key outside horizontal bottom center
set xtics nomirror
set ytics nomirror
set format y '10^{%T}'
set ylabel ARG2
set title ARG1

set x2tics rangelimited rotate by 55 format '88-88'

good = ARG4
bad = ARG5

set terminal dumb
set output '/dev/null'

#  noautoscale with csplines was ignored
set clip two
plot \
'ftot' u 1:2 notitle w points, \
'' u 1:3 notitle w points, \
'xtot' u 1:2 notitle w points, \
'' u 1:3 notitle w points, \
'xtvm' u 1:2 notitle w points, \
'xdday' u 1:2 notitle axis x1y2 w points, \
'dday' u 1:2 notitle axis x1y2 w points

set xrange   [GPVAL_X_MIN:GPVAL_X_MAX]
set x2range [GPVAL_X2_MIN:GPVAL_X2_MAX]
set yrange   [GPVAL_Y_MIN:GPVAL_Y_MAX]
set y2range [GPVAL_Y2_MIN:GPVAL_Y2_MAX]

set terminal pngcairo size 820,440 transparent
#set output 'test.png'
#test

#print GPVAL_X_MIN
#print GPVAL_X_MAX

# 86400 = 24 * 60 * 60; 2678400 = 31 * 86400
interval = int((GPVAL_X_MAX - GPVAL_X_MIN)/2678400) + 1
#set x2tics interval * 86400 format ''
set x2tics interval
set mx2tics interval

# only label Mondays
set xtics '2020-01-06', 604800

# break x and x2 axis
# 280800 = 86400 * 3.25; 324000 + 86400 * 3.75
set obj 1 \
  rect from GPVAL_X_MAX - 324000,graph 0 to GPVAL_X_MAX - 280800,graph 1 \
  fc 'gray' fs transparent pattern 6 noborder bgnd back

# shade alternating weeks

#     January 1970
# Su Mo Tu We Th Fr Sa
#              1  2  3
# 604800 = 60 * 60 * 24 * 7; 1209600 = 604800 * 2

# day of week offset
dowo = 86400 * (int(GPVAL_X_MAX / 86400 + 2.5) % 7 + 4)

set for [i=GPVAL_X_MAX-dowo:GPVAL_X_MIN:-1209600] obj i \
  rect from i-604800,graph 0 to i,graph 1 \
  fc 'gray' fs transparent noborder bgnd back

# Draw a y axis at current date
# 345600 = 86400 * 4
#set x2range[-(GPVAL_X_MAX - GPVAL_X_MIN - 345600):345600]
set x2range[-(GPVAL_X_MAX - GPVAL_X_MIN - 345600)/86400:4]
set y2zeroaxis ls -1 lc rgbcolor 'gray'

#print GPVAL_Y_MIN
#print GPVAL_Y_MAX

set logscale y
set y2range [GPVAL_Y2_MAX:GPVAL_Y2_MIN]

# with leap days taken into account
set y2tics (\
      '.25'  .25  1,\
       '.5'   .5  1,\
      '.75'  .75  1,\
        '1'    1   ,\
'1.~3{.8-}' 4.0/3 1,\
'1.~6{.8-}' 5.0/3 1,\
        '2'    2   ,\
      '2.5'  2.5  1,\
        '3'    3   ,\
        '4'    4   ,\
        '5'    5  1,\
        '6'    6  1,\
        '7'    7   ,\
        '8'    8  1,\
        '9'    9  1,\
       '10'   10  1,\
       '11'   11  1,\
       '12'   12  1,\
       '14'   14   ,\
       '21'   21  1,\
       '30'   30   ,\
       '60'   60  1,\
       '91'   91   ,\
      '182'  182  1,\
      '273'  273  1,\
      '365'  365   ,\
      '730'  730  1,\
     '1095' 1095  1,\
   '4'.yr_t 1461   ,\
   '8'.yr_t 2922  1,\
  '12'.yr_t 4383   ,\
  '16'.yr_t 5844  1,\
  '20'.yr_t 7306   ,\
  '40'.yr_t 14610 1,\
  '60'.yr_t 21915 1,\
  '80'.yr_t 29220 1,\
 '100'.yr_t 36524  ,\
 '200'.yr_t 73048  1,\
 '300'.yr_t 109572 1,\
 '400'.yr_t 146097 )
set y2tics add (sprintf("\n_{%.1f}", GPVAL_Y2_MAX) GPVAL_Y2_MAX)

# make g() log10 from 10-100
f = 90.0/(GPVAL_Y2_MAX - GPVAL_Y2_MIN); o = 10 - f*GPVAL_Y2_MIN
set nonlinear y2 via log10(f*y+o) inverse (10**y-o)/f

interval_s = sprintf("%d", interval)
#print interval_s
`./x2tics n=@interval_s x2tics.txt`

set grid x2 y2

set output 'a.png'

plot \
'ftot' u 1:2:3 w filledcurves above \
  fs trans solid 0.25 border fc rgbcolor good notitle, \
'' u 1:2:3 w filledcurves below \
  fs trans solid 0.25 border fc rgbcolor bad notitle, \
'xtot' u 1:2:3 notitle w filledcurves above \
  fs trans solid 0.25 border fc rgbcolor good, \
'' u 1:2:3 notitle w filledcurves below \
  fs trans solid 0.25 border fc rgbcolor bad, \
'tot' u 1:2 w step lt 0 notitle, \
'' u 1:2 w points lt -1 pt 1 ps .5 notitle, \
'xtot' u 1:2 with l lt 0 notitle, \
'xtvm' u 1:2 with l lt -1 lc 1 dt 2 notitle, \
'xdday' using 1:2 axis x1y2 with lines lt 0 lc 1 notitle, \
'daily' u 1:2 w step lt 4 notitle, \
'dday' u 1:2 axis x1y2 w lines smooth csplines lt 0 lc 1 notitle, \
'' u 1:2 axis x1y2 w points lt 0 lc 1 pt 1 ps .5 notitle, \
'xtotl' u 1:2:3 w labels left offset 0,-.25 font ',8' notitle,\
'' u 1:2 w points lt -1 pt 1 ps .5 notitle,\
'xl' u 1:2:3 axis x1y2 w labels left offset 0,-.25 font ',8' notitle, \
'' u 1:2 axis x1y2 w linespoints lt 0 lc 1 pt 1 ps .5 title trend_t, \
'ol' using 1:2:3 w labels left offset .5,.5 font ',8' \
  point pt 1 notitle, \
'labels' u 1:2:3 w labels right offset -.5,1.5 font ',8' notitle, \
'' u 1:2 w points lt -1 pt 1 notitle
