set xdata time
set timefmt '%Y-%m-%d-%H'
set grid
#set xtics rangelimited rotate by -55 format '%1m/%1d'
set autoscale fix
set xtics rotate by -55 format '%1m/%1d'
set xlabel ARG3.' day doubling-time'
set key outside horizontal bottom center
set ytics nomirror
set logscale y
set format y '10^{%T}'
set ylabel ARG2
#set logscale y2
#set y2range [*:*] reverse
set title ARG1
set y2label 'doubling-time (hours)'
set y2tics 24
set my2tics 2
set terminal dumb
set output '/dev/null'
plot \
'ftot' using 1:2:3 notitle with filledcurves above \
  fs trans solid 0.25 border fc 'red', \
'ftot' using 1:2:3 notitle with filledcurves below \
  fs trans solid 0.25 border fc 'green', \
'dhrs' using 1:2 axis x1y2 with lines lt -1 title 'lifetime =', \
'tot' using 1:2 with lines lt 0 notitle, \
'5' using 1:2 axis x1y2 with points title '5 sample =', \
'10' using 1:2 axis x1y2 with points title '10 sample =', \
'15' using 1:2 axis x1y2 with points title '15 sample =', \
'labels' using 1:2:3 with labels right offset -.5,-.5 font ',8' point pt 1 \
  notitle

# 86400 = 24 * 60 * 60; 2678400 = 31 * 86400
interval = int((GPVAL_X_MAX - GPVAL_X_MIN)/2678400) + 1
set xtics interval * 86400
set mxtics interval

set terminal pngcairo size 820,440
set output 'a.png'
refresh
