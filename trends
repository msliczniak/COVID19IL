#set terminal dumb
#set terminal unknown
set output '/dev/null'
set table '/dev/null'

#set print '-'

set xdata time
set timefmt '%Y-%m-%d-%H:%M:%S'
set autoscale fix

# figure ranges
plot 'table' index 1 ax x2y2 w points\
, 'ftot' every 2 u 1:2 w points\
, '' every 2 u 1:4 w points

# x does not change; 86400 = 24 * 60 * 60
set xrange [GPVAL_X_MIN:GPVAL_X_MAX + 86400]
set x2range [(GPVAL_X_MIN - GPVAL_X_MAX)/86400:1]

max = GPVAL_Y2_MAX
min = GPVAL_Y2_MIN

# figure doubling-time range
plot '' every 2 u 1:2 ax x1y1 w points\
, '' every 2 u 1:3 ax x1y1 w points\
, '' every 2 u 1:4 ax x1y1 w points\
, 'table' index 0 ax x2y2 w points

unset table
set terminal pngcairo size 1640,440 transparent
set output 'a.png'

# Draw a y axis at current date
set y2zeroaxis ls -1 lc rgbcolor 'gray'
set y2tics border out

# 2678400 = 31 * 86400
set x2tics rotate by 55 format '88-88'
interval = int((GPVAL_X_MAX - GPVAL_X_MIN)/2678400) + 1
set x2tics GPVAL_X2_MIN - int(GPVAL_X2_MIN) % interval, interval
set mx2tics interval

# only label Mondays
set xtics '2020-01-06', 604800

# shade alternating weeks
#     January 1970
# Su Mo Tu We Th Fr Sa
#              1  2  3
# 604800 = 60 * 60 * 24 * 7; 1209600 = 604800 * 2
# day of week offset
dowo = 86400 * (int(GPVAL_X_MAX / 86400 + 2.5) % 7 + 4)
set for [i=GPVAL_X_MAX-dowo+604800:GPVAL_X_MIN:-1209600] obj i\
  rect from i-604800,graph 0 to i,graph 1\
  fc 'gray' fs transparent noborder bgnd back

ymin = GPVAL_Y_MIN
ymax = GPVAL_Y_MAX

# start with the percentages one band under the totals
f = ceil(log10(ymax)) - ceil(log10(max)) - 1
p = 10**f

# never let totals under 10^0
if (ymin < 1) { ymin = 1 }

# slide-up bottom of percentages if necessary
o = min*p
if (o < ymin) {
	o = floor(log10(ymin)) - floor(log10(o))
	f = f + o
	p = 10**f
}

# extend yrange if needed
o = max*p
if (o > ymax) { ymax = o }

set yrange [ymin:ymax]
set logscale y

# line-up percentages and totals
set for [i = ceil(log10(ymin)) : floor(log10(ymax))] ytics \
( sprintf("10^{%d}\n_{%s%%%%}", i, gprintf("%h", 10**(i - f))) 10**i\
, '' 10**i + 10*10**(i - 1) 1\
, '' 10**i + 20*10**(i - 1) 1\
, '' 10**i + 30*10**(i - 1) 1\
, '' 10**i + 40*10**(i - 1) 1\
, '' 10**i + 50*10**(i - 1) 1\
, '' 10**i + 60*10**(i - 1) 1\
, '' 10**i + 70*10**(i - 1) 1\
, '' 10**i + 80*10**(i - 1) 1\
)

# set apart the logscale tics
set ytics scale 2, 1

# draw arrows to show which label corresponds with each tic
o = 1
while (o <= ymax) {
	if (o >= ymin) {
		set arrow from graph 0,first o rto character -4 backhead\
		  back lt -1 lc rgb 'grey'
	}
	o = o * 10
}

# 2.5 25 250...
o = 2.5
while (o <= ymax) {
	if (o >= ymin) {
		set arrow from graph 0,first o rto graph 1 nohead\
	  	  back lt -1 lc rgb 'grey' dt '    -    '
	}
	o = o * 10
}

# 5 50 500...
o = 5
while (o <= ymax) {
	if (o >= ymin) {
		set arrow from graph 0,first o rto graph 1 nohead\
	  	  back lt -1 lc rgb 'grey' dt ' -  '
	}
	o = o * 10
}

# clamp to 400y
f = GPVAL_Y2_MAX > 146097 ? 146097 : GPVAL_Y2_MAX

# spread-out tics
set y2range [f:GPVAL_Y2_MIN]
o = GPVAL_Y2_MAX - GPVAL_Y2_MIN
f = 10**ceil(log10(o) + 1)
f = (f - 10.0)/o
o = 10 - f*GPVAL_Y2_MIN
set nonlinear y2 via log10(f*y+o) inverse (10**y-o)/f

# with leap days taken into account
set y2tics (\
      '.25'  .25  1,\
       '.5'   .5  1,\
      '.75'  .75  1,\
         1     1   ,\
'1.~3{.8-}' 4.0/3 1,\
'1.~6{.8-}' 5.0/3 1,\
        '2'    2   ,\
      '2.5'  2.5  1,\
        '3'    3   ,\
        '4'    4   ,\
        '5'    5  1,\
        '6'    6  1,\
        '7'    7   ,\
        '8'    8  1,\
        '9'    9  1,\
       '10'   10  1,\
       '11'   11  1,\
       '12'   12  1,\
       '14'   14   ,\
       '21'   21  1,\
       '30'   30   ,\
       '60'   60  1,\
       '91'   91   ,\
      '182'  182  1,\
      '273'  273  1,\
      '365'  365   ,\
      '730'  730  1,\
     '1095' 1095  1,\
   '4'.yr_t 1461   ,\
   '8'.yr_t 2922  1,\
  '12'.yr_t 4383   ,\
  '16'.yr_t 5844  1,\
  '20'.yr_t 7306   ,\
  '40'.yr_t 14610 1,\
  '60'.yr_t 21915 1,\
  '80'.yr_t 29220 1,\
 '100'.yr_t 36524  ,\
 '200'.yr_t 73048  1,\
 '300'.yr_t 109572 1,\
 '400'.yr_t 146097 )
set y2tics add (sprintf("^{%.1f}\n", GPVAL_Y2_MIN) GPVAL_Y2_MIN)
set y2tics add (sprintf("\n_{%.1f}", GPVAL_Y2_MAX) GPVAL_Y2_MAX)
set y2tics border out

interval_s = sprintf("%d", interval)
`./x2tics n=@interval_s x2tics.txt`

set grid x2 y
set xtics nomirror
set ytics mirror
set border 2 + 8 back lc rgbcolor 'gray'
set key outside horizontal bottom center font ',8'
set ylabel ARG2
set y2label ARG3 . ' ' . trend_t . ' ' . y2label_t
set title ARG1
good = ARG4
bad = ARG5
set clip two

plot '' index 0 u 1:($1 <= -7 ? $2 : NaN) ax x2y2 sm mcspline notitle\
, '' index 0 u 1:($1 >= -7 ? $2 : NaN) ax x2y2 sm mcspline lt 1 dt '-'\
  title trend_t\
, 'ftot' u 1:2:4 w filledcurves above\
 fs trans solid 0.25 border fc rgbcolor good notitle\
, '' u 1:2:4 w filledcurves below\
 fs trans solid 0.25 border fc rgbcolor bad notitle\
, '' every 2 u 1:2 w points lt -1 pt 1 ps .5 notitle\
, '' u 1:3 w step lt 4 title daily_t\
, '' u 1:($4 > $2) ? $4 : NaN w lines lc rgbcolor bad notitle\
, '' u 1:($4 <= $2) ? $4 : NaN w lines lc rgbcolor good notitle\
, 'daily' ax x2y1 sm bezier lt 0 lc 6 lw 2 notitle\
, 'dday' u 1:($3 <= 0 ? .1 : $3*p) ax x2y1 lt -1 sm mcspline title '%'\

