set xlabel ARG3.xlabel_t

set xdata time
set timefmt '%Y-%m-%d-%H'
set autoscale fix
set key outside horizontal bottom center
set xtics nomirror
set ytics nomirror
set format y '10^{%T}'
set ylabel ARG2
set title ARG1

set x2tics rangelimited rotate by 55 format '88-88'

good = ARG4
bad = ARG5

set terminal dumb
set output '/dev/null'
plot \
'ftot' u 1:2:3 w filledcurves above \
  fs trans solid 0.25 border fc rgbcolor good notitle, \
'' u 1:2:3 w filledcurves below \
  fs trans solid 0.25 border fc rgbcolor bad notitle, \
'fdday' using 1:2:3 axis x1y2 notitle with filledcurves below \
  fs trans solid 0.25 border fc rgbcolor good, \
'fdday' using 1:2:3 axis x1y2 notitle with filledcurves above \
  fs trans solid 0.25 border fc rgbcolor bad, \
'xtot' using 1:2:3 notitle with filledcurves above \
  fs trans solid 0.25 border fc rgbcolor good, \
'xtot' using 1:2:3 notitle with filledcurves below \
  fs trans solid 0.25 border fc rgbcolor bad, \
'xdday' using 1:2:3 axis x1y2 notitle with filledcurves below \
  fs trans solid 0.25 border fc rgbcolor good, \
'xdday' using 1:2:3 axis x1y2 notitle with filledcurves above \
  fs trans solid 0.25 border fc rgbcolor bad, \
'tot' u 1:2 w step lt 0 notitle, \
'' u 1:2 w points lt -1 pt 1 ps .5 notitle, \
'x5t' using 1:2 with lines lt -1 lc 1 dt 4 notitle, \
'xtot' using 1:2 with lines lt 0 notitle, \
'x5' using 1:2 with lines lt -1 lc 1 dt 2 notitle, \
'dday' using 1:2 axis x1y2 with lines lt -1 title lifetime_t, \
'xdday' using 1:2 axis x1y2 with lines lt -1 notitle, \
'5' using 1:2 axis x1y2 with linespoints lt 0 pt 1 lc 1 ps .5 title trend_t, \
'xtotl' using 1:2:3 w labels left offset 0,-.5 font ',8' point pt 11 notitle,\
'xl' using 1:2:3 axis x1y2 w labels left offset 0,-.5 font ',8' \
  point pt 24 notitle, \
'ol' using 1:2:3 w labels left offset .5,.5 font ',8' \
  point pt 11 notitle, \
'labels' u 1:2:3 w labels right offset -.5,2 font ',8' notitle, \
'' u 1:2 w points lt -1 pt 1 notitle

#\
'10' using 1:2 axis x1y2 with points pt 2 lc 2 title '10 sample =', \
'15' using 1:2 axis x1y2 with points pt 3 lc 3 title '15 sample =', \
'x15' using 1:2 with lines lt -1 lc 3 dt 2 notitle, \
'x10' using 1:2 with lines lt -1 lc 2 dt 2 notitle, \

set terminal pngcairo size 820,440 transparent
#set output 'test.png'
#test

#print GPVAL_X_MIN
#print GPVAL_X_MAX

# 86400 = 24 * 60 * 60; 2678400 = 31 * 86400
interval = int((GPVAL_X_MAX - GPVAL_X_MIN)/2678400) + 1
#set x2tics interval * 86400 format ''
set x2tics interval
set mx2tics interval

# only label Mondays
set xtics '2020-01-06', 604800

# break x and x2 axis
# 280800 = 86400 * 3.25; 324000 + 86400 * 3.75
set obj 1 \
  rect from GPVAL_X_MAX - 324000,graph 0 to GPVAL_X_MAX - 280800,graph 1 \
  fc 'gray' fs transparent pattern 6 noborder bgnd back

# shade alternating weeks

#     January 1970
# Su Mo Tu We Th Fr Sa
#              1  2  3
# 604800 = 60 * 60 * 24 * 7; 1209600 = 604800 * 2

# day of week offset
dowo = 86400 * (int(GPVAL_X_MAX / 86400 + 2.5) % 7 + 4)

set for [i=GPVAL_X_MAX-dowo:GPVAL_X_MIN:-1209600] obj i \
  rect from i-604800,graph 0 to i,graph 1 \
  fc 'gray' fs transparent noborder bgnd back

# Draw a y axis at current date
# 345600 = 86400 * 4
#set x2range[-(GPVAL_X_MAX - GPVAL_X_MIN - 345600):345600]
set x2range[-(GPVAL_X_MAX - GPVAL_X_MIN - 345600)/86400:4]
set y2zeroaxis ls -1

#print GPVAL_Y_MIN
#print GPVAL_Y_MAX

set y2range [GPVAL_Y2_MIN:GPVAL_Y2_MAX + 100]

set logscale y
set logscale y2

# with leap days taken into account
set y2tics (\
      '.25'  .25  1,\
       '.5'   .5  1,\
      '.75'  .75  1,\
        '1'    1   ,\
'1.~3{.8-}' 4.0/3 1,\
'1.~6{.8-}' 5.0/3 1,\
        '2'    2   ,\
      '2.5'  2.5  1,\
        '3'    3   ,\
        '4'    4   ,\
        '5'    5  1,\
        '6'    6  1,\
        '7'    7   ,\
        '8'    8  1,\
        '9'    9  1,\
       '10'   10  1,\
       '11'   11  1,\
       '12'   12  1,\
       '14'   14   ,\
       '21'   21  1,\
       '30'   30   ,\
       '60'   60  1,\
       '91'   91   ,\
      '182'  182  1,\
      '273'  273  1,\
      '365'  365   ,\
      '730'  730  1,\
     '1095' 1095  1,\
       '4y' 1461   ,\
       '8y' 2922  1,\
      '12y' 4383   ,\
      '16y' 5844  1,\
      '20y' 7306   ,\
      '40y' 14610 1,\
      '60y' 21915 1,\
      '80y' 29220 1,\
     '100y' 36524  ,\
     '200y' 73048  1,\
     '300y' 109572 1,\
     '400y' 146097)

interval_s = sprintf("%d", interval)
#print interval_s
`./x2tics n=@interval_s x2tics.txt`

set grid x2 y2

set output 'a.png'
refresh
