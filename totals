#!/usr/bin/awk -f

# reqs: only print cur if bigger than prev and zero but print biggest last

BEGIN {
	# BSD awk can only do single char RS
	FS = "[][}{,]|\"[ \t]*:[ \t]*"

	key = "County"
	select = "Illinois"
	result = "confirmed cases"
}

NR == 1 {
	key = "\"" tolower(key) "$"

	# prevent gnuplot from using subscripts in enhanced title
	gsub(/ /, "_", result)
	result = "\"" result "$"

	if (substr(select, 1, 1) == "!") {
		select = substr(select, 2)
		s = 1
	}

	gsub(/,/, "|", select)
	select = "\"(" tolower(select) ")\""
}

FNR == 1 {
	if (t > l) {
		print d, t, k
		l = t
	}
	k++

	# strip-off path
	d = match(FILENAME, "[^/]*$")
	d = substr(substr(FILENAME, d), 1)

	# get date
	if (d !~ /^[1-9][0-9][0-9][0-9]-/)
		d = substr(d, index(d, "-") + 1, 10)
	else
		d = substr(d, 1, 10)

	t = 0
}

!s && tolower($1) ~ key && tolower($2) ~ select { found = 1; next }

s && tolower($1) ~ key && tolower($2) !~ select { found = 1; next }

found && tolower($1) ~ result { t += $2; found = 0 }

END { if (l > t) t = l; print d, t, k }
